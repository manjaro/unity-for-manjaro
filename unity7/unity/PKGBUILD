# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=kinetic
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.6.0
_extra_ver=+22.10.20220913
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
epoch=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(appstream-glib bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs
         gnome-desktop gnome-screensaver gnome-session-unity unity-settings-daemon
         gsettings-ubuntu-schemas gsettings-desktop-schemas-ubuntu hud ido libgnomeui
         libindicator-gtk2 libindicator-gtk3 libnotify libunique libunity libunity-misc
         libxfixes nux unity-asset-pool libxi lightdm-unity-greeter dbus-activation-env
         zeitgeist-ubuntu python-pyxdg notify-osd gnome-keyring)
makedepends=(chrpath cmake doxygen intltool patchutils pkg-config
             python-setuptools flex libglvnd-1.4.0)
groups=(unity)

#options=(!ccache)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.tar.xz"
        0004-Remove-social.scope.patch
        0005-Remove-xpathselect-dependency.patch
        filemanager.patch
        launcher_bfb.svg
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam)
sha512sums=('SKIP'
            'cbeece65a35545e2f6704d568e0253f4370684ea935b9cbcaa388892a0f0848be2a022998502d5da31f4a68651fd4df095b80803cc3166ab72cd01a5cc712213'
            'SKIP'
            'SKIP'
            'd3ba5c513e1a07f10d5851d79498f31e81d573dcb271a2a36f70cd2cd525c0cf454048bcd78a07782792cfdcc052153a69ce3ef5d7710355b9d698f8858d2120'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            'SKIP')
options=(!strip !docs libtool emptydirs)

prepare() {
    cd unity

    patch -p1 -i ../0004-Remove-social.scope.patch
    patch -p1 -i ../0005-Remove-xpathselect-dependency.patch
    patch -p1 -i ../filemanager.patch

    # Apply Ubuntu patches
    for i in $(grep -v '#' debian/patches/series); do
        patch -p1 -i "debian/patches/${i}"
    done

    sed -i \
        -e 's/"Ubuntu Desktop"/"Manjaro Unity"/' \
        panel/PanelMenuView.cpp

    rm -f resources/launcher_bfb.svg
    cp ../launcher_bfb.svg resources
}

build() {
    cd unity

    export CXXFLAGS+=" -g -O0"

    # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
    export CCACHE_SLOPPINESS=time_macros

    [[ -d build ]] && rm -rvf build/
    mkdir build/ && cd build/

    cmake -Wno-dev \
        -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
        -DCOMPIZ_PACKAGING_ENABLED=TRUE \
        -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
        -DUSE_GSETTINGS=TRUE \
        -DENABLE_UNIT_TESTS=FALSE \
        ..

    make -j$(nproc)

    # Make sure that the GSettings schema files were created
    gen=generated/glib-2.0/schemas
    if \
        [ ! -f ${gen}/org.compiz.networkarearegion.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitydialog.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitymtgrabhandles.gschema.xml ] ||
        [ ! -f ${gen}/org.compiz.unityshell.gschema.xml ]; then
        make compiz_gsettings_compile_local
    fi
}

unity-panel-service_dosym() {
    local x
    for x in $2; do
        mkdir -p ${pkgdir}/usr/lib/systemd/user/$1.service.wants
        ln -s ${pkgdir}/usr/lib/systemd/user/indicator-${x}.service ${pkgdir}/usr/lib/systemd/user/$1.service.wants/indicator-${x}.service
    done
}

package() {
    cd unity/build
    make DESTDIR="${pkgdir}/" install

    mkdir -p ${pkgdir}/usr/lib/systemd/user/unity-session.target.{wants,requires}

    ln -s ${pkgdir}/usr/lib/systemd/user/unity7.service ${pkgdir}/usr/lib/systemd/user/unity-session.target.requires/unity7.service
    ln -s ${pkgdir}/usr/lib/systemd/user/unity-settings-daemon.service ${pkgdir}/usr/lib/systemd/user/unity-session.target.wants/unity-settings-daemon.service
    ln -s ${pkgdir}/usr/lib/systemd/user/window-stack-bridge.service ${pkgdir}/usr/lib/systemd/user/unity-session.target.wants/window-stack-bridge.service

    # Top panel systemd indicator services required for unity-panel-service #
    unity-panel-service_dosym "unity-panel-service" "application bluetooth datetime keyboard messages power printers session sound"
    # Top panel systemd indicator services required for unity-panel-service-lockscreen #
    unity-panel-service_dosym "unity-panel-service-lockscreen" "datetime keyboard power session sound"

    cp ${srcdir}/unity.pam ${pkgdir}/etc/pam.d/unity

    mkdir -p ${pkgdir}/usr/share/unity-control-center/keybindings
    ln -s ${pkgdir}/usr/share/gnome-control-center/keybindings/50-unity-launchers.xml ${pkgdir}/usr/share/unity-control-center/keybindings/50-unity-launchers.xml
}
