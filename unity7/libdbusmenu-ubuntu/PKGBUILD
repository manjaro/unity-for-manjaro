# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=libdbusmenu, repo=kinetic
# vercheck-launchpad: name=dbusmenu, tarname=libdbusmenu

pkgbase=libdbusmenu-ubuntu
pkgname=(libdbusmenu-glib-ubuntu libdbusmenu-gtk3-ubuntu)
_actual_ver=16.04.1
_extra_ver=+22.10.20220928.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet"
arch=(i686 x86_64)
url="https://launchpad.net/dbusmenu"
license=(LGPL)
makedepends=(json-glib gtk3 intltool gtk-doc gobject-introspection vala
             valgrind gtk2)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libdbusmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch)
sha512sums=('SKIP'
            '303eae41659c29a78bd1f24aa7a01b35987049f6213d23fc6ecd5a7e72109d277fd6d443414750274f1fc75cee567285ca8d9d49887e6ab19e36565630fdf08b')

prepare() {
    #cd "libdbusmenu-${_actual_ver}${_extra_ver}"

    patch -p1 -i 0001_autoconf.patch
}

build() {
    #cd "libdbusmenu-${_actual_ver}${_extra_ver}"

    export CFLAGS+=" -Wno-error"

    sed -i 's/gtk+-2.0 >= $GTK_REQUIRED_VERSION//' configure.ac

    gtkdocize
    autoreconf -vfi
    intltoolize -f

    sed -i 's|$(DBUSMENUGTK_CFLAGS)|$(DBUSMENUGTK_CFLAGS) -L../libdbusmenu-glib/.libs|' libdbusmenu-gtk/Makefile.{am,in}

    [[ -d build-gtk3 ]] || mkdir build-gtk3
    pushd build-gtk3
    ../configure --prefix=/usr --libexecdir=/usr/lib/libdbusmenu --disable-scrollkeeper --with-gtk=3 --disable-static
    make
    popd
}

package_libdbusmenu-glib-ubuntu() {
    depends=(json-glib)
    provides=(libdbusmenu-glib)
    replaces=(libdbusmenu-glib)
    conflicts=(libdbusmenu-glib)

    #cd "libdbusmenu-${_actual_ver}${_extra_ver}/build-gtk3"
    cd build-gtk3

    make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}/" install
    make -j1 -C tools DESTDIR="${pkgdir}/" install
    make -j1 -C tests DESTDIR="${pkgdir}/" install
    make -j1 -C docs/libdbusmenu-glib DESTDIR="${pkgdir}/" install
    make -j1 -C po DESTDIR="${pkgdir}/" install
}

package_libdbusmenu-gtk3-ubuntu() {
    pkgdesc+=" (GTK+ 3 library)"
    depends=(gtk3 libdbusmenu-glib-ubuntu)
    provides=(libdbusmenu-gtk3)
    replaces=(libdbusmenu-gtk3)
    conflicts=(libdbusmenu-gtk3)

    #cd "libdbusmenu-${_actual_ver}${_extra_ver}/build-gtk3"
    cd build-gtk3

    make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}/" install
}
