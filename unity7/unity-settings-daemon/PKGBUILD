# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=kinetic

pkgname=unity-settings-daemon
_ubuntu_rel=0ubuntu1
_actual_ver=15.04.1
_extra_ver=+21.10.20220802
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
pkgdesc="Unity Settings Daemon"
arch=(i686 x86_64)
url="https://launchpad.net/unity-settings-daemon"
license=(GPL)
groups=(unity)
depends=(gnome-settings-daemon-ubuntu gsettings-desktop-schemas-ubuntu fcitx
         gperf hicolor-icon-theme ibus libappindicator-gtk3 libcanberra-pulse
         libnotify librsvg libsystemd libwacom libxkbfile mesa pulseaudio
         pulseaudio-alsa upower gsettings-ubuntu-schemas)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl)
conflicts=(gnome-desktop-compat upower-compat)
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Remove-accountsservice-dependency.patch
        0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
        0003-Use-GNOME-3.16-deprecated-schemas.patch
        0001-usd-test-screensaver-proxy-fails-to-compile.patch)
sha512sums=('SKIP'
            'SKIP'
            '2394d35355f31a1090c96a38f3e33930d70aa1683f1bc3c4b65fd6a70a9bf9e5a592109ed34dd2d5a07822b401a24d11d80a662aae850236434d89c37a9a9c89'
            'deafb16a268d660614dfa37cdbccc5ab8489327e7d61d766dd24f3304bebea3f8991d335d4332e33deea66e12ed4efcee6a606d9638c7457fef39b29f71dea77'
            '4dc034674a417680c1d0ed46fe3345a31412a9ffbc6a65242566c55fb4d81b166affb1c32a187706e31dd6b10868b9032431ce846e8e06a4b5949bcc99ec3641'
            '0b6fa66ebecef51c54ca04ae5aec6c6e05450668865e544f4c5ed3b142dcb22301bae440f55dfeec8fc5534a3c818d8cba4d019c32f5e3441a588bea9b93dab8')

prepare() {
    cd unity-settings-daemon-${_actual_ver}${_extra_ver}

    patch -p1 -i ../unity-settings-daemon_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff

    patch -p1 -i ../0001-Remove-accountsservice-dependency.patch
    #patch -p1 -i 0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch
    #patch -p1 -i 0003-Use-GNOME-3.16-deprecated-schemas.patch
    patch -p1 -i ../0001-usd-test-screensaver-proxy-fails-to-compile.patch
}

build() {
    cd unity-settings-daemon-${_actual_ver}${_extra_ver}

    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/unity-settings-daemon \
        --disable-static \
        --enable-systemd \
        --enable-fcitx

    # https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j$(nproc)
}

package() {
    cd unity-settings-daemon-${_actual_ver}${_extra_ver}

    make DESTDIR="${pkgdir}/" install

    install -dm755 "${pkgdir}/usr/bin/"
    ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
        "${pkgdir}/usr/bin/unity-settings-daemon"

    install -Dm644 debian/unity-settings-daemon.user-session.upstart \
        "${pkgdir}"/usr/share/upstart/sessions/unity-settings-daemon.conf
    install -Dm644 debian/unity-settings-daemon.user-session.desktop \
        "${pkgdir}"/usr/share/upstart/xdg/autostart/unity-settings-daemon.desktop

    install -dm755 "${pkgdir}"/usr/lib/systemd/user/
    install -m644 debian/user/unity-settings-daemon.service \
        "${pkgdir}"/usr/lib/systemd/user/

    # Required until unity7 gets a systemd unity with Requires=u-s-d
    install -dm755 "${pkgdir}"/usr/lib/systemd/user/ubuntu-session.target.wants/
    ln -s /usr/lib/systemd/user/unity-settings-daemon.service \
        "${pkgdir}"/usr/lib/systemd/user/ubuntu-session.target.wants/

    install -dm755 "${pkgdir}"/usr/share/upstart/systemd-session/upstart/
    install -m644 debian/user/unity-settings-daemon.override \
        "${pkgdir}"/usr/share/upstart/systemd-session/upstart/

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"

    rm -f "${pkgdir}"/usr/lib/udev/rules.d/61-gnome-settings-daemon-rfkill.rules
    rm -rv "${pkgdir}"/usr/share/upstart/
}
