# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=kinetic
# vercheck-launchpad: name=${pkgname}

pkgname=libunity
_actual_ver=7.1.4
_extra_ver=+19.04.20190319
_ubuntu_rel=6build1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=(i686 x86_64)
url="https://launchpad.net/libunity"
license=(LGPL)
depends=(dee-ubuntu libdbusmenu-glib gtk3)
makedepends=(intltool vala python2 gobject-introspection)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001_autoconf.patch
        libunity-ubuntu-patches.patch)
sha512sums=('SKIP'
            'SKIP'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b'
            'SKIP')

prepare() {
    find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

    patch -p1 -i 0001_autoconf.patch

    # Apply Ubuntu patches
    # patch -p1 -i "${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
    patch -p1 -i libunity-ubuntu-patches.patch

    for i in $(grep -v '#' debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "debian/patches/${i}"
    done
}

build() {
    intltoolize -f
    autoreconf -vfi
    ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
    make -j1
}

package() {
    make DESTDIR="${pkgdir}/" install
}
